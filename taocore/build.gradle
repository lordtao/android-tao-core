apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = "1.2.8"
group = "ua.at.tsvetkov"

def versionMajor = 1
def versionMinor = 2
def versionPatch = 8

def siteUrl = 'https://github.com/lordtao/android-tao-core'
def gitUrl = 'https://github.com/lordtao/android-tao-core.git'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 12
        versionName version
//        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
//        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
}

dependencies {
    compile 'com.android.support:support-annotations:23.1.1'
}

// Hide properties from ext users
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "android-tao-core"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["BSD 3-Clause"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Android App Configurator, easy Log with detailed information'
                url siteUrl
                licenses {
                    license {
                        name 'BSD 3-Clause'
                        url 'http://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                developers {
                    developer {
                        id 'lordtao'
                        name 'Alexandr Tsvetkov'
                        email 'tsvetkov2010@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}